buildscript {
    repositories {
        mavenCentral()
        //Needed only for SNAPSHOT versions
        //maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}

plugins {
    id 'net.researchgate.release' version '2.4.0'
    id 'net.saliman.cobertura' version '2.3.1'
    id 'com.github.kt3k.coveralls' version '2.6.3'
}

apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = "com.github.adesigns"

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'

    // this is used for cobertura to work properly, otherwise throws a bunch of NoClassDef
    testRuntime "org.slf4j:slf4j-log4j12:1.7.5"
    testRuntime "log4j:log4j:1.2.7"
}

closeRepository.dependsOn uploadArchives
afterReleaseBuild.dependsOn closeAndPromoteRepository

cobertura.coverageFormats = ['html', 'xml'] // coveralls plugin depends on xml format report

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }


            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                username = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : ""
                authentication(userName: username, password: password)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                username = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : ""
                authentication(userName: username, password: password)
            }

            pom.project {
                name 'CSV Creator'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Dead-simple way to generate CSV output.'
                url 'https://github.com/adesigns/csv-creator'

                scm {
                    connection 'scm:git@github.com:adesigns/csv-creator.git'
                    developerConnection 'scm:git@github.com:adesigns/csv-creator.git'
                    url 'https://github.com/adesigns/csv-creator'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'mikeyudin'
                        name 'Michael Yudin'
                        email 'chat@albanydesigns.com'
                    }
                }
            }
        }
    }
}